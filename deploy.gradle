apply plugin: 'maven'
apply plugin: 'signing'

if (project.hasProperty("android")) {
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else {
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

configurations {
    deployerJars
}

repositories {
    mavenCentral()
}
dependencies{
    // https://mvnrepository.com/artifact/org.codehaus.plexus/plexus-classworlds
    deployerJars group: 'org.codehaus.plexus', name: 'plexus-classworlds', version: '2.4'
    deployerJars group: 'org.apache.maven.wagon', name: 'wagon-ssh', version: '2.12'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            configuration = configurations.deployerJars
            repository(url: "scp://maven.ogiqvo.com/home/xor/maven-repositories/staging"){
                authentication(userName: "xor", privateKey: "/home/xor/.ssh/id_rsa")
            }

            pom.project {
                packaging 'jar'

                name 'vtm'
                description 'OpenGL vector map library written in Java - running on Android, iOS, Desktop and within the browser.'
                url 'https://github.com/schedul-xor/vtm'

                licenses {
                    license {
                        name 'GNU Lesser GPL'
                        url 'http://www.gnu.org/licenses/lgpl.html'
                        distribution 'repo'
                    }
                }

                scm {
                    connection 'scm:git:https://github.com/schedul-xor/vtm'
                    developerConnection 'scm:git:https://github.com/schedul-xor/vtm'
                    url 'https://github.com/schedul-xor/vtm'
                }

                developers {
                    developer {
                        id 'hannes.janetzek'
                        name 'Hannes Janetzek'
                        email 'hannes.janetzek@gmail.com'
                    }
                    developer {
                        id 'devemux86'
                        name 'Emux'
                        email 'devemux86@gmail.com'
                    }
                    developer {
                        id 'schedul-xor'
                        name 'Izumi Kawashima'
                        email 'xor@ogiqvo.com'
                    }
                }
            }
        }
    }
}
