apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'application'

dependencies {
    api project(':vtm-gdx')
    file('natives').eachDir() { dir ->
        api files(dir.path)
    }
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    implementation 'com.metsci.ext.com.kitfox.svg:svg-salamander:0.1.19'
    implementation "org.slf4j:slf4j-api:$slf4jVersion"
}

sourceSets {
    main {
        java.srcDirs = ['src']
    }
}

file('natives').eachDir() { dir ->
    task("nativesJar-${dir.name}", type: Jar) {
        classifier = "natives-${dir.name}"
        from(dir.path)
    }

    artifacts {
        archives tasks["nativesJar-${dir.name}"]
    }
}

mainClassName = 'org.oscim.gdx.GdxMapApp'
run { ignoreExitValue = true }

// Automated Gradle project deployment to Sonatype OSSRH
afterEvaluate {
    project.apply from: "${rootProject.projectDir}/vtm-desktop/deploy.gradle"
}
